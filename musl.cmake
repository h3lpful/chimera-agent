if(NOT CMAKE_BUILD_TYPE)
    set(CMAKE_BUILD_TYPE Release)
endif()
if(NOT CMAKE_SYSTEM_NAME)
    set(CMAKE_SYSTEM_NAME Linux)
endif()
if(NOT ARCH)
    set(ARCH "x86_64")
endif()
if(NOT SYSTEM_NAME)
    set(SYSTEM_NAME "linux")
endif()

set(TOOLCHAIN "${ARCH}-${SYSTEM_NAME}-musl")
if(NOT EXTERNAL_DIR)
    set(EXTERNAL_DIR ${PROJECT_BINARY_DIR}/external)
endif()
set(TOOLCHAIN_DIR ${EXTERNAL_DIR}/${TOOLCHAIN}-cross)
set(CHAIN_TEMP ${EXTERNAL_DIR}/chain.tar.gz)
set(TOOLCHAIN_PREFIX ${TOOLCHAIN_DIR}/bin/${TOOLCHAIN})

if(NOT EXISTS ${TOOLCHAIN_DIR})
    file(DOWNLOAD
        https://musl.cc/${TOOLCHAIN}-cross.tgz
        ${CHAIN_TEMP}
        SHOW_PROGRESS
    )
    file(
        ARCHIVE_EXTRACT
        INPUT ${CHAIN_TEMP}
        DESTINATION ${EXTERNAL_DIR}
    )
    file(REMOVE ${CHAIN_TEMP})
endif()

SET(CMAKE_C_COMPILER ${TOOLCHAIN_PREFIX}-gcc)
SET(CMAKE_C_COMPILER_LINKER ${TOOLCHAIN_PREFIX}-ld)
SET(CMAKE_CXX_COMPILER ${TOOLCHAIN_PREFIX}-g++)
SET(CMAKE_CXX_COMPILER_LINKER ${TOOLCHAIN_PREFIX}-ld)
SET(CMAKE_STRIP ${TOOLCHAIN_PREFIX}-strip)

SET(CMAKE_SYSTEM_INCLUDE_PATH ${TOOLCHAIN_DIR})
SET(CMAKE_SYSROOT ${TOOLCHAIN_DIR})
SET(CMAKE_FIND_ROOT_PATH  ${TOOLCHAIN_DIR})
SET(CMAKE_FIND_ROOT_PATH_MODE_PROGRAM NEVER)
set(CMAKE_FIND_ROOT_PATH_MODE_PACKAGE ONLY)
SET(CMAKE_FIND_ROOT_PATH_MODE_LIBRARY ONLY)
SET(CMAKE_FIND_ROOT_PATH_MODE_INCLUDE ONLY)